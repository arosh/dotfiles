# Ctrl-^ で cd ..
function cdup() {
  echo
  cd ..
  zle reset-prompt
}
zle -N cdup
bindkey '^\^' cdup

# not foundのとき、apt-getを推薦したりする
if [ -f /etc/zsh_command_not_found ]; then
  source /etc/zsh_command_not_found
fi

# http://www.clear-code.com/blog/2011/9/5.html
if type lv > /dev/null 2>&1; then
    ## lvを優先する。
    export PAGER="lv"
else
    ## lvがなかったらlessを使う。
    export PAGER="less"
fi

if [ "$PAGER" = "lv" ]; then
    ## -c: ANSIエスケープシーケンスの色付けなどを有効にする。
    ## -l: 1行が長くと折り返されていても1行として扱う。
    ##     （コピーしたときに余計な改行を入れない。）
    export LV="-c -l"
else
    ## lvがなくてもlvでページャーを起動する。
    alias lv="$PAGER"
fi

# http://d.hatena.ne.jp/mollifier/20100317/p1
# ファイルの内容をクリップボードにコピー
clip() {
  # $1がファイルのとき
  if [ -f $1 ] ; then
    if which pbcopy >/dev/null 2>&1 ; then
      # Mac
      cat $1 | pbcopy
    elif which xsel >/dev/null 2>&1 ; then
      # Linux
      cat $1 | xsel --input --clipboard
    elif which putclip >/dev/null 2>&1 ; then
      # Cygwin
      cat $1 | putclip
    fi
  else
    echo "'$1' is not a valid file!"
  fi
}

# VCSのブランチ名を表示
# http://d.hatena.ne.jp/mollifier/20090814/p1
autoload -Uz vcs_info
zstyle ':vcs_info:*' formats '(%s)-[%b]'
zstyle ':vcs_info:*' actionformats '(%s)-[%b|%a]'
# svn用の設定？
precmd () {
    psvar=()
    LANG=en_US.UTF-8 vcs_info
    [[ -n "$vcs_info_msg_0_" ]] && psvar[1]="$vcs_info_msg_0_"
}
export RPROMPT="%1(v|%F{yellow}%1v%f|)"

# 怠惰な解凍コマンド
# extract http://d.hatena.ne.jp/jeneshicc/20110215/1297778049
ex () {
  if [ -f $1 ] ; then
      case $1 in
          *.zip)            unzip $1 -d ${1%.zip}  ;;
          *.tar)            tar xvf $1     ;;
          *.tar.gz|*.tgz)   tar xvzf $1    ;;
          *.tar.bz2|*.tbz2) tar xvjf $1    ;;
          *.tar.xz)         tar xvJf $1    ;;
          *.bz2)            bunzip2 $1     ;;
          *.rar)            unrar x $1     ;;
          *.gz)             gunzip $1      ;;
          *.Z)              uncompress $1  ;;
          *.7z)             7z x $1        ;;
          *.lzma)           lzma -dv $1    ;;
          *.xz)             xz -dv $1      ;;
          *.lzh)            lha e $1       ;;
          *)                echo "don't know how to extract '$1'..." ;;
      esac
  else
      echo "'$1' is not a valid file!"
  fi
}

# ターミナルで256色表示できているか確認
function 256colortest() {
    local code
    for code in {0..255}; do
        echo -e "\e[38;05;${code}m $code: Test"
    done
}

# https://github.com/zsh-users/zsh-completions
# fpath=($HOME/git/zsh-completions/src $fpath)
